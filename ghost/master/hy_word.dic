notification_sound : all
{
	if !ISVAR("volume"); volume = 50
	if !ISVAR("notif"); notif = "None"
	
	//Skip if empty, or if set to custom and also empty...
	if !(notif == "None" || notif == "" || (notif == "Custom" && custom_sound_path == ""))
	{
		"\![sound,play,"""
		
		if notif == "Custom"
		{
			custom_sound_path
		}
		elseif notif == "Random"
		{
			_opts = ("Water_Drop","Bubbles","Splash","Campana","Stop","Success")
			_opts = "snd/" + _opts + ".mp3"
			if Config.IncludeCustomSound && custom_sound_path != ""; _opts ,= custom_sound_path
			ANY(_opts)
		}
		else
		{
			"snd/" + REPLACE(notif," ","_") + ".mp3"
		}
		
		""",--volume=%(volume),--window=false]"
	}
}

OnTranslate
{
	_talk = reference0
	
	_talk = REPLACE(_talk,"\b[4]","\b[4,--fallback=2]")
	
	if reference1 == "" && reference2 == ""
	{ //send input box : no event (ref2) , no special flag (ref1)
		EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
	}
    else
	{ //event result from ghost
		TOSTR(_talk)
	}
}

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}