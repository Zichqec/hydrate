OnTranslate
{
	_talk = reference0
	
	//Process embedded elements in script input
	if reference1 == "" && reference2 == ""
	{
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	_talk = REPLACE(_talk,"\b[4]","\b[4,--fallback=2]")
	
	TOSTR(_talk)
}

ghostver
{
	"2.0.2"
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/hydrate/refs/heads/main/"
}

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

OnSurfaceRestore
{
	"\0\s[0]"
}

OnWindowStateMinimize
{
	OnHydrateGhost.GetStats //This is the ONE place so far that I have to call this directly... if I go to another event it does not work
}

OnWindowStateRestore
{
	OnSendStats
	--
	OnSurfaceRestore
}

OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t" && SHIORI3FW.DebugMode; OnAITalk
}

OnMinuteChange
{
	if minute == 0
	{
		if hour == 0
		{
			AutoUpdates.CheckedToday = 0
			AutoUpdates.GhostReady = 0
			AutoUpdates.ShellReady = 0
			AutoUpdates.BalloonReady = 0
		}
		
		OnSendStats
		--
		SendOldStats //Deprecated version
		--
		if Config.HourNotifs; HourNotification
	}
	--
	if Config.AutoShellChange == "Every interval"
	{
		if !(BalloonIsOpen || "choosing" _in_ status) && GETSECCOUNT > AutoShell.LastChangeTime + (Config.AutoShellHours * 3600)
		{
			"\![change,shell,random,--option=raise-event]"
			return //Don't do the checks below until after changing shells... I don't think this will come up but just in case
		}
	}
	elseif Config.AutoDressupChange == "Every interval"
	{
		if !(BalloonIsOpen || "choosing" _in_ status) && GETSECCOUNT > AutoDressup.LastChangeTime + (Config.AutoDressupHours * 3600)
		{
			"\![embed,OnRandomDressup]"
		}
	}
	--
	//Once a day the ghost will check for updates if the user has auto updates on
	if Config.AutoUpdates && AutoUpdates.CheckedToday == 0
	{
		AutoUpdates.GhostReady = 0
		AutoUpdates.ShellReady = 0
		AutoUpdates.BalloonReady = 0
		"\![update,ghost+shell+balloon,checkonly]"
	}
}

OnSecondChange
{
	//Reminders when minimized - for cases such as being overstimulated and needed to minimize what you have open, but still wanting water reminders! No streak or drink tracking available in this mode...
	if IsMinimized && Config.RemindWhenMinimized
	{
		//Based on the aitalkinterval stuff in the shiori3.dic file...
		if aitalkinterval > 0
		{
			_sec = GETSECCOUNT
			_diff = _sec - SHIORI3FW.LastAITalkTime
			if _diff >= aitalkinterval
			{
				if (_sec - SHIORI3FW.LastTalkTime) >= 6
				{
					if (_sec - SHIORI3FW.TalkEndTime) >= 6
					{
						if SHIORI3FW.IsAITalkComplete == 0
						{
							//Send a notif banner with the appropriate text...
							//Adjusting the last talk time here because that way it doesn't screw with the timing of your reminders
							_dialogue = ReminderTalk
							_command = "\![set,trayballoon,--text=""%(_dialogue)"",--title=Hydrate reminder,--icon=info]"
							
							SHIORI3FW.PushAdditionalReturn('ValueNotify',_command)
							SHIORI3FW.ResetAITalkInterval(0)
						}
					}
				}
			}
		}
	}
	
	//Create arrays if none exist
	if !ISVAR("ErrorsToPush.level"); ErrorsToPush.level = IARRAY
	if !ISVAR("ErrorsToPush.description"); ErrorsToPush.description = IARRAY

	if ARRAYSIZE(ErrorsToPush.level) > 0 //If there are errors
	{
		_levels = ""
		_descriptions = ""
		for _i = 0; _i < ARRAYSIZE(ErrorsToPush.level); _i++ //CHR(1) in here is the divider between errors
		{
			if _i > 0; {_levels += CHR(1); _descriptions += CHR(1)}
			_levels += ErrorsToPush.level[_i]
			_descriptions += ErrorsToPush.description[_i]
		}
		//Push the whole list of errors together
		SHIORI3FW.PushAdditionalReturn('ErrorLevel',_levels)
		SHIORI3FW.PushAdditionalReturn('ErrorDescription',_descriptions)
		
		//Clear the arrays so we don't send any more errors later
		ErrorsToPush.level = IARRAY
		ErrorsToPush.description = IARRAY
	}
}

OnShellScaling
{
	Scale.X = reference0
	Scale.Y = reference2
	
	OnSendStats
	--
	"\0\s[0](%(reference0)% hydration.)"
}

OnUserInputCancel
{
	if reference0 == "OnConfig.AutoShell.HourSet"; OnConfig.AutoShell
	elseif reference0 == "OnDrinkTime"; OnConfigMenu
}


//—————————————————————————————— Owner draw menu ——————————————————————————————
On_recommendrootbutton.caption
{
	"More from the devs"
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites)
}

recommendsites : array
{
	//Use craftman if craftmanw is not present (but it always should be! it's a mandatory field! please use craftmanw)
	//TODO check with Pona, this might be unnecessary
	_shellauthor = shellauthor[0]
	if _shellauthor == ""; _shellauthor = shellauthor[1]
	
	"'%(SHIORI3FW.ShellName)' shell by %(_shellauthor)"; "%(shellauthor[2])"
	"Zi's Ukagaka Space"; "https://ukagaka.zichqec.com/"
	"Zdzisiu's website"; "https://zdzisiu.github.io/Cemetery/index.html"
}
 
On_portalrootbutton.caption
{
	"Reference sites"
}

On_sakura.portalsites
{
	FormatLinks(portalsites)
}

portalsites : array
{
	"SSP"; "http://ssp.shillest.net/"
	"Ukadoc"; "https://ukagakadreamteam.github.io/ukadoc/manual/index.html"
	"AYAYA"; "https://ukagakadreamteam.github.io/ayaya/index"
}

On_vanishbutton.visible
{
	if Config.EnableUninstall; 1
	else; 0
}