//—————————————————————————————— Installing something ——————————————————————————————
OnInstallComplete 
{
	"\0\s[0](Installed %(reference0): %(reference1))."
	--
	if "ghost" _in_ reference0
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,call]Call %(reference0)\__q  \![*]\__q[OnInstallComplete.Action,change]Change to %(reference0)\__q\n\n"
		--
		"\![*]\__q[OnBlank]Done\__q"
	}
	elseif "shell" _in_ reference0
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,changeshell]Change to %(reference0)\__q\n\n"
		--
		"\![*]\__q[OnBlank]Done\__q"
	}
}

OnInstallComplete.Action
{
	if reference0 == "changeshell"; "\![change,shell,lastinstalled,--option=raise-event]"
	elseif reference0 == "change"; "\![change,ghost,lastinstalled]"
	else; "\![call,ghost,lastinstalled]"
}

OnInstallFailure
{
	_reason = reference0
	case reference0
	{
		when "unlha32"; _reason = "could not load the dll to decompress lzh files"
		when "extraction"; _reason = "failed to decompress the file"
		when "invalid type"; _reason = "the install.txt is incorrect"
		when "unsupported"; _reason = "the archive is not supported"
		when "password"; _reason = "incorrect password"
		when "artificial"; _reason = "canceled by user"
	}
	
	"\0\s[0](Could not complete installation: %(_reason).)"
}

OnInstallRefuse
{
	"\0\s[0](Cannot install, this is meant for %(reference0).)"
}


//—————————————————————————————— Creating updates/nar files ——————————————————————————————
OnUpdatedataCreating
{
	"\0\s[0](Creating an updates2.dau file.)"
}

OnUpdatedataCreated
{
	"\0\s[0](Finished making the updates2.dau.)"
}

OnNarCreating
{
	"\0\s[0](Creating a .nar file.)"
}

OnNarCreated
{
	//Makes a clickable link that'll open the location of the file they just made
	_nar = "\_a[OnNarLocation,""%(reference1)""]%(reference0)\_a"
	
	"\0\s[0](Successfully created %(_nar).)"
}

OnNarLocation
{
	"\![open,explorer,""%(reference0)""]"
}


//—————————————————————————————— Uninstallation ——————————————————————————————
OnVanishSelecting
{
	"\0\s[0](Do you want to uninstall?)"
}

OnVanishSelected 
{
	"\0\s[0](Take care of yourself, user!)\w8\w8"
}

OnVanished
{
	"\0\s[0](%(reference0) wasn't hydrated enough.)"
}


//—————————————————————————————— Network Update ——————————————————————————————
//When the ghost finishes checking for updates (for the auto update check)
OnUpdateCheckResult
{
	AutoUpdates.CheckedToday = 1 //Sets it so it won't check again until at least midnight
	AutoUpdates.GhostReady = 0
	AutoUpdates.ShellReady = 0
	AutoUpdates.BalloonReady = 0
	
	//One reference per thing checked for, so go through and find which ones have updates ready and put them in an array...
	_types = IARRAY
	foreach reference; _ref
	{
		_ref = SPLIT(_ref,C_BYTE1)
		if _ref[1] == "OK" && _ref[2] > 0; _types ,= _ref[0]
	}
	
	if ASEARCH("ghost",_types) != -1; AutoUpdates.GhostReady = 1
	if ASEARCH("shell",_types) != -1; AutoUpdates.ShellReady = 1
	if ASEARCH("balloon",_types) != -1; AutoUpdates.BalloonReady = 1
}

OnStartUpdate
{
	_cmd = ""
	if AutoUpdates.GhostReady == 1; _cmd += "ghost+"
	if AutoUpdates.ShellReady == 1; _cmd += "shell+"
	if AutoUpdates.BalloonReady == 1; _cmd += "balloon"
	_cmd = ERASE(_cmd,(STRLEN(_cmd) - 1),1)
	"\![update,%(_cmd)]"
}

OnUpdateBegin
{
	"\0\s[0](Beginning update.)\w8\w8"
}

OnUpdateReady
{
	//Number of files starts from 0, so this displays the correct amount
	reference0++
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "is"
	if reference0 != 1; {_s = "s"; _are = "are"}
	
	"\0\s[0](There %(_are) %(reference0) new file%(_s).)\w8"
}

OnUpdateComplete : all
{
	OnUpdateVars
	
	if reference0 == "none"
	{
		"\0\s[0](There are no new files.)"
	}
	else
	{
		AutoUpdates.CheckedToday = 0
		"\0\s[0](Update complete.)"
		"\_q\n\n"
		"\![*]\__q[OnChangeLog]Show changelog\__q\n"
		"\![*]\__q[OnBlank]Done\__q"
	}
}

OnChangeLog : all
{
	"\j[https://ukagaka.zichqec.com/ghost/hydrate#version_history]"
}

OnUpdateFailure //If the update fails
{
	_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //For MD5 errors, this gets the name and extension of the offending file
	_reason = reference0
	case reference0
	{
		when "timeout"; _reason = "connection timed out"
		when "md5 miss"; _reason = "MD5 error on file %(_file)\n\nPlease contact \_a[https://ukagaka.zichqec.com/]Zichqec\_a for assistance"
		when "artificial"; _reason = "canceled by user"
	}
	"\0\s[0](Could not update: %(_reason).)"
}

OnUpdateOtherBegin
{
	if reference3 == "balloon"
	{
		"(Checking for updates to the current balloon, %(SHIORI3FW.BalloonName).)"
	}
	elseif reference3 == "shell"
	{
		"(Checking for updates to the current shell, %(SHIORI3FW.ShellName).)"
	}
	else
	{
		"(Beginning %(reference3) update.)"
	}
	--
	"\w8"
}

OnUpdateOtherReady
{
	reference0++
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "is"
	if reference0 != 1; {_s = "s"; _are = "are"}
	
	"\0\s[0](There %(_are) %(reference0) new file%(_s).)\w8"
}

OnUpdateOtherComplete
{
	if reference0 == "none"
	{
		"\0\s[0](No new files for the current %(reference3) to update with.)"
	}
	else
	{
		AutoUpdates.CheckedToday = 0
		"\0\s[0](Update complete!)"
	}
	
}

OnUpdateOtherFailure
{
	"\0(%(reference3) update failed: '%(reference0)'.)"
}


//—————————————————————————————— SNTP (clock fixing) ——————————————————————————————
OnSNTPCompare
{
	if reference3 != 0
	{
		_s = ""; if reference3 != 1; _s = "s"
		
		"\0\s[0](The clock is off by %(reference3) second%(_s).)\n\n\_q"
		--
		"\![*]\__q[script:\6]Fix it\__q  \![*]\__q[OnBlank]Leave it\__q"
	}
	else
	{
		"\0\s[0](The clock is accurate.)"
	}
}

OnSNTPCorrect
{
	"\0\s[0](The clock has been corrected.)"
}

OnSNTPFailure
{
	"\0\s[0](Could not correct the clock.)"
}


//—————————————————————————————— BIFF (Email check) ———————————————————————————————
OnBIFFFailure
{
	_reason = reference0
	case reference0
	{
		when "timeout"; _reason = "connection timed out"
		when "kick"; _reason = "can't access account"
		when "defect"; _reason = "POP settings incorrect"
		when "artificial"; _reason = "canceled by user"
	}

	"\0\s[0](Could not get emails: %(_reason).)"
}


//———————————————————————————————— Headlines/RSS ——————————————————————————————————
OnHeadlinesenseFailure
{
	_reason = reference0
	case reference0
	{
		when "can't download"; _reason = "can't download the file"
		when "can't analyze"; _reason = "can't analyze the file"
	}

	"\0\s[0](Could not get RSS: %(_reason).)"
}