OnAITalk
{
	_talk = ReminderTalk
	if SHIORI3FW.ShellName == "Alkohol"; _talk = Wobble(_talk,0,0,currentdrinkstreak)
	_talk = "\0\s[0]%(notification_sound)" + _talk
	_talk += "\![set,choicetimeout,%(TimeoutInterval)]\n\n\![*]\q[Done,OnDrankWater]\n\n[half]\![*]\q[Dismiss,OnDismissWater]\e drinknotif"
	_talk
}

ReminderTalk : nonoverlap
{
	"(It's time to drink some water!)"
	"(You should take a sip of water.)"
	"(Stay hydrated! Take a sip of water if you haven't lately.)"
	"(It's time for a sip of water!)"
	"(You should get a drink!)"
	"(Get a drink and move around a little!)"
}

OnDrankWater : nonoverlap
{
	todayswaters++
	totalwaters++
	currentdrinkstreak++
	
	if !ISVAR("highestdrinkstreak"); highestdrinkstreak = 0
	if !ISVAR("newhighscorelatch"); newhighscorelatch = 0
	_newhighscorecomment = 0
	
	//Unset latch if it's gotten mixed up somehow and we logically *cannot* be on a new high score streak
	if newhighscorelatch == 1 && currentdrinkstreak < highestdrinkstreak; newhighscorelatch = 0
	
	//Don't start saying this until you get 3 in a row I think??
	//This will completely not track a streak unless you get at least 3...
	if currentdrinkstreak > highestdrinkstreak && currentdrinkstreak >= 3 //TODO maybe? i should?? make it save this even before you hit 3? hm. decide later, but it should match how the current streak is displayed
	{
		highestdrinkstreak = currentdrinkstreak
		
		if newhighscorelatch == 0
		{
			_newhighscorecomment = 1
			newhighscorelatch = 1 //suppress further comments until streak is broken
		}
	}
	
	_talk = ""
	if currentdrinkstreak == 3; _talk = StreakStartDrink
	elseif _newhighscorecomment; _talk = NewStreakHighScoreDrink
	elseif totalwaters % 100 == 0; _talk = MilestoneDrink
	elseif currentdrinkstreak % 5 == 0; _talk = StreakDrink //TODO maybe add a further comment if you're on a high score streak...? hm
	else; _talk = GeneralDrink
	
	if SHIORI3FW.ShellName == "Alkohol"; _talk = Wobble(_talk,0,0,currentdrinkstreak)
	_talk = "\0\s[0]" + OnSendStats + _talk
	
	_amt = currentdrinkstreak * 100
	if _amt > 10000; _amt = 10000 //100 max - lets not do a hyponatremia
	if _amt == 800; _amt = 820
	_talk += "\![raiseplugin,c58b6320-caf8-11ed-a901-0800200c9a66,OnWalletOfUnyu,set,%(_amt),Thanks for hydrating!]"
	
	if Config.AutoShellChange == "After a drink"
	{
		_talk += "\_w[3000]\![change,shell,random,--option=raise-event]"
	}
	elseif Config.AutoDressupChange == "After a drink"
	{
		_talk += "\_w[3000]\![embed,OnRandomDressup]"
	}
	_talk
}

OnDismissWater
{
	currentdrinkstreak = 0
	newhighscorelatch = 0
	OnSendStats
}

OnChoiceTimeout
{
	if "drinknotif" _in_ reference0
	{
		currentdrinkstreak = 0
		newhighscorelatch = 0
		OnSendStats
	}
}

StreakStartDrink : nonoverlap
{
	"(You've reached a streak of drinks, good job! Keep it up!)"
	"(You've started a streak! Keep going!)"
	"(You're doing really well, you've started a streak!)"
	"(3 drink combo! Keep going!)"
}

GeneralDrink : nonoverlap
{
	"(Good job!)"
	"(Well done!)"
	"(Keep it up!)"
	"(Excellent!)"
	"(Great!)"
	"(Thank you!)"
	"(Heck yeah,\w4 hydration!)"
	"(Awesome!)"
	"(You're great!)"
	"(You're rad!)"
	"(Fresh!)"
	"(You did it!)"
	"(Powerful!)"
	"(Look at you go!)"
}

StreakDrink : nonoverlap
{
	"(That's %(currentdrinkstreak) drinks in a row! Way to go!)"
	"(%(currentdrinkstreak) strong! You're doing awesome!)"
	"(%(currentdrinkstreak) so far! Awesome!)"
	"(You've had %(currentdrinkstreak) so far! That's really great, good job!)"
}

MilestoneDrink : nonoverlap
{
	"(You've reached %(totalwaters) total timed drinks, good job!)"
	"(You've reached %(totalwaters) total timed drinks, you're a hydration pro!)"
	"(You've reached %(totalwaters) total timed drinks, fantastic!)"
	"(You've reached %(totalwaters) total timed drinks, look at you go!)"
}

NewStreakHighScoreDrink
{
	"(You've reached a new high score of %(highestdrinkstreak) drinks in a row! Way to go!)"
	"(New high score! %(highestdrinkstreak) drinks in a row! Incredible!)"
}